#!groovy

pipeline {
	agent {
		dockerfile {
			filename 'Dockerfile.jenkins'
			// 'docker build' would normally copy the whole build-dir to the container, changing the
			// docker build directory avoids that overhead
			dir 'docker'
			// Pass the uid and the gid of the current user (jenkins-user) to the Dockerfile, so a
			// corresponding user can be added. This is needed to provide the jenkins user inside
			// the container for the ssh-agent to work.
			// Another way would be to simply map the passwd file, but would spoil additional information
			additionalBuildArgs '--build-arg USER_ID=$(id -u) --build-arg GROUP_ID=$(id -g)'
			args "--device /dev/kvm:/dev/kvm -v /var/local/container_shared/gradle/:/.gradle -v /var/local/container_shared/android-sdk:/usr/local/android-sdk -v /var/local/container_shared/android-home:/.android -v /var/local/container_shared/emulator_console_auth_token:/.emulator_console_auth_token -v /var/local/container_shared/analytics.settings:/analytics.settings"
		}
	}

	environment {
		//////// Define environment variables to point to the correct locations inside the container ////////
		//////////// Most likely not edited by the developer
		ANDROID_SDK_ROOT = "/usr/local/android-sdk"
		// Deprecated: Still used by the used gradle version, once gradle respects ANDROID_SDK_ROOT, this can be removed
		ANDROID_HOME = "/usr/local/android-sdk"
		ANDROID_SDK_HOME = "/"
		// Needed for compatibiliby to current Jenkins-wide Envs. Can be removed, once all builds are migrated to Pipeline
		ANDROID_SDK_LOCATION = "/usr/local/android-sdk"
		ANDROID_NDK = ""
		// This is important, as we want the keep our gradle cache, but we can't share it between containers
		// the cache could only be shared if the gradle instances could comunicate with each other
		// imho keeping the cache per executor will have the least space impact
		GRADLE_USER_HOME = "/.gradle/${env.EXECUTOR_NUMBER}"
		// Otherwise user.home returns ? for java applications
		JAVA_TOOL_OPTIONS = "-Duser.home=/tmp/"

		//// jenkins-android-helper related variables
		// set to any value to debug jenkins_android* scripts
		ANDROID_EMULATOR_HELPER_DEBUG = ""
		// get stdout of called subprocesses immediately
		PYTHONUNBUFFERED = "true"

		//////// Build specific variables ////////
		//////////// May be edited by the developer on changing the build steps
	}

	options {
		timeout(time: 2, unit: 'HOURS')
		timestamps()
	}

	stages {
		stage('Prepare build') {
			steps {
				script {
					def configAsMarkup = ""
					env.EMUALTOR_CONFIG.each {
						// WORKAROUND: eachLine does not work atm
						if (it == '\n' || it == '\r')
                            configAsMarkup = configAsMarkup + "<br>"
						else
						    configAsMarkup = configAsMarkup + it
					}

					currentBuild.displayName = "${env.CLASS_PKG_TO_TEST}"
					currentBuild.description = """Build was run with the following options:<br>
<b>Repo</b>: ${env.REPO}<br>
<b>Branch</b>: ${env.BRANCH}<br>
<b>Tests</b>: ${env.CLASS_PKG_TO_TEST}<br>
<br>
<b>====== Emulator Config ======</b><br>
${configAsMarkup}<br>
<b>=============================</b>"""
				}
				sh "if [ -n \"${env.EMUALTOR_CONFIG}\" ]; then echo \"${env.EMUALTOR_CONFIG}\" > ./buildScripts/emulator_config.ini; fi"
			}
		}

		stage('Setup Android SDK') {
			steps {
				// Install Android SDK
				lock("update-android-sdk-on-${env.NODE_NAME}") {
					sh "./buildScripts/build_step_install_android_sdk"
				}
			}
		}

		stage('Unit and Device tests') {
			steps {
				sh "./buildScripts/build_helper_run_tests_on_emulator ${env.CLASS_PKG_TO_TEST}"
			}

			post {
				always {
					junit '**/*TEST*.xml'

					// stop/kill emulator
					sh "./buildScripts/build_helper_stop_emulator"
				}
			}
		}
	}

	post {
		always {
			step([$class: 'LogParserPublisher', failBuildOnError: true, projectRulePath: 'buildScripts/log_parser_rules', unstableOnWarning: true, useProjectRule: true])
		}
	}
}
