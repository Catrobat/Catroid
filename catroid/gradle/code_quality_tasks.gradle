/*
 * Catroid: An on-device visual programming system for Android devices
 * Copyright (C) 2010-2022 The Catrobat Team
 * (<http://developer.catrobat.org/credits>)
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as
 * published by the Free Software Foundation, either version 3 of the
 * License, or (at your option) any later version.
 *
 * An additional term exception under section 7 of the GNU Affero
 * General Public License, version 3, is available at
 * http://developer.catrobat.org/license_additional_term
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'checkstyle'
apply plugin: 'pmd'

check.dependsOn 'checkstyle'
check.dependsOn 'pmd'
check.dependsOn 'detekt'

dependencies {
    pmd 'net.sourceforge.pmd:pmd-core:6.55.0'
    pmd 'net.sourceforge.pmd:pmd-java:6.55.0'

    checkstyle 'com.puppycrawl.tools:checkstyle:10.12.1'

    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:1.23.0"
}

task checkstyle(type: Checkstyle) {
    configDirectory = file(".")
    configFile = file("./config/checkstyle.xml")
    source '.'
    include '**/*.java', '**/*.xml', '**/*.gradle'
    exclude '**/gen/**', '**/build/**', 'libraryProjects/**', '**/.idea/**'

    classpath = files()

    // needed for console output of warnings/errors
    showViolations true
    ignoreFailures true

    reports {
        xml.required = true
        xml.destination file("build/reports/checkstyle.xml")
    }
}

task pmd(type: Pmd) {
    ruleSetFiles = files('config/pmd.xml')
    ruleSets = []

    source '.'
    include '**/*.java'
    exclude '**/gen/**', '**/build/**', '**/res/**'

    ignoreFailures true

    reports {
        xml.required = true
        html.enabled = false
        xml.destination file("build/reports/pmd.xml")
    }
}

detekt {
    toolVersion = "1.23.0"
    config = files("config/detekt.yml")
    input = files("src")
    ignoreFailures true
}
