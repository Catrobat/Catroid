/*
 * Catroid: An on-device visual programming system for Android devices
 * Copyright (C) 2010-2022 The Catrobat Team
 * (<http://developer.catrobat.org/credits>)
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as
 * published by the Free Software Foundation, either version 3 of the
 * License, or (at your option) any later version.
 *
 * An additional term exception under section 7 of the GNU Affero
 * General Public License, version 3, is available at
 * http://developer.catrobat.org/license_additional_term
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'jacoco'

jacoco {
    toolVersion = jacoco_core_version
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}

project.afterEvaluate {

    android.applicationVariants.all { variant ->
        def variantName = variant.name.capitalize()
        def kotlinClassSubdirectory = variant.name

        def testTaskName = "test${variantName}UnitTest"

        tasks.create(name: "jacocoTest${variantName}UnitTestReport", type: JacocoReport, dependsOn: "$testTaskName") {
            group = "Reporting"
            description = "Generate Jacoco coverage reports for the ${variantName} build."

            reports {
                csv.required = false
                html.required = true
                xml.required = true
            }

            def excludes = [
                    '**/R.class',
                    '**/R$*.class',
                    '**/BuildConfig.*',
                    '**/Manifest*.*',
                    '**/*Test*.*',
                    'android/**/*.*'
            ]
            def javaClasses = fileTree(dir: variant.javaCompiler.destinationDir, excludes: excludes)
            def kotlinClasses = fileTree(
                    dir: "${buildDir}/tmp/kotlin-classes/${kotlinClassSubdirectory}",
                    excludes: excludes)
            getClassDirectories().from(files([javaClasses, kotlinClasses]))

            getSourceDirectories().from(files([
                    "$project.projectDir/src/main/java",
                    "$project.projectDir/src/${variantName}/java",
                    "$project.projectDir/src/main/kotlin",
                    "$project.projectDir/src/${variantName}/kotlin"
            ]))

            getExecutionData().from(files(
                    "${project.buildDir}/code_coverage/**/*.ec",
                    "${project.buildDir}/outputs/unit_test_code_coverage/${variant.name}UnitTest/${testTaskName}.exec",
            ))
        }
    }
}
