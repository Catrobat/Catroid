/*
 * Catroid: An on-device visual programming system for Android devices
 * Copyright (C) 2010-2017 The Catrobat Team
 * (<http://developer.catrobat.org/credits>)
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as
 * published by the Free Software Foundation, either version 3 of the
 * License, or (at your option) any later version.
 *
 * An additional term exception under section 7 of the GNU Affero
 * General Public License, version 3, is available at
 * http://developer.catrobat.org/license_additional_term
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'
        classpath 'io.fabric.tools:gradle:1.21.5'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
    }
}

repositories {
    jcenter()
    maven { url "https://jitpack.io" }
    maven { url 'https://maven.fabric.io/public' }
    maven { url 'https://maven.google.com' }
}

ext {
    appId = 'org.catrobat.catroid'
    appName = '@string/app_name'
    manifestAppName = '@string/app_name'
    manifestAppIcon = '@drawable/ic_launcher'
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'com.neenbedankt.android-apt'

apply from: 'gradle/code_quality_tasks.gradle'
apply from: 'gradle/intellij_config_tasks.gradle'
apply from: 'gradle/standalone_apk_tasks.gradle'

check.dependsOn 'checkstyle'
check.dependsOn 'pmd'

// When -Pindependent was provided on the gradle command the APP name is changed.
// This allows to have multiple Catroid versions installed in parallel for testing purposes.
// Furthermore these installations do not interfere with the actual Catroid app.
if (project.hasProperty('independent')) {
    def today = new Date()
    appId += '.independent_' + today.format('YYYYMMdd_HHmm')
    appName = property('independent') ?: 'Code ' + today.format('MMdd HH:mm')
    manifestAppName = appName
}
ant.copy(file: 'google-services-template.json', tofile: 'google-services.json', overwrite: true)
ant.replace(file: 'google-services.json', token: '@appId@', value: appId)

android {
    dexOptions {
        javaMaxHeapSize "4g"
    }
    packagingOptions {
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
        exclude 'META-INF/INDEX.LIST'  //compile problem with parrot arsdk
        exclude 'lib/arm64-v8a/*' // exclude all arm64-v8a libs as we don't have all libs for that architecture
    }

    buildTypes {
        debug {
            buildConfigField "boolean", "FEATURE_LEGO_NXT_ENABLED", "true"
            buildConfigField "boolean", "FEATURE_LEGO_EV3_ENABLED", "true"
            buildConfigField "boolean", "FEATURE_PHIRO_ENABLED", "true"
            buildConfigField "boolean", "FEATURE_PARROT_AR_DRONE_ENABLED", "true"
            buildConfigField "boolean", "FEATURE_PARROT_JUMPING_SUMO_ENABLED", "true"
            buildConfigField "boolean", "FEATURE_USERBRICKS_ENABLED", "true"
            buildConfigField "boolean", "FEATURE_SCRATCH_CONVERTER_ENABLED", "true"
            buildConfigField "boolean", "FEATURE_NFC_ENABLED", "true"
            buildConfigField "boolean", "FEATURE_ARDUINO_ENABLED", "true"
            buildConfigField "boolean", "FEATURE_RASPI_ENABLED", "true"
            buildConfigField "boolean", "FEATURE_POCKETMUSIC_ENABLED", "true"
            buildConfigField "boolean", "FEATURE_CAST_ENABLED", "true"
            buildConfigField "boolean", "FEATURE_CLOUD_MESSAGING_ENABLED", "true"
            buildConfigField "boolean", "CRASHLYTICS_CRASH_REPORT_ENABLED", "true"
            resValue "string", "SNACKBAR_HINTS_ENABLED", "false"
            ext.enableCrashlytics = false
        }
        release {
            buildConfigField "boolean", "FEATURE_LEGO_NXT_ENABLED", "true"
            buildConfigField "boolean", "FEATURE_LEGO_EV3_ENABLED", "true"
            buildConfigField "boolean", "FEATURE_PHIRO_ENABLED", "true"
            buildConfigField "boolean", "FEATURE_PARROT_AR_DRONE_ENABLED", "true"
            buildConfigField "boolean", "FEATURE_PARROT_JUMPING_SUMO_ENABLED", "true"
            buildConfigField "boolean", "FEATURE_SCRATCH_CONVERTER_ENABLED", "true"
            buildConfigField "boolean", "FEATURE_USERBRICKS_ENABLED", "false"
            buildConfigField "boolean", "FEATURE_ARDUINO_ENABLED", "true"
            buildConfigField "boolean", "FEATURE_RASPI_ENABLED", "true"
            buildConfigField "boolean", "FEATURE_NFC_ENABLED", "true"
            buildConfigField "boolean", "FEATURE_POCKETMUSIC_ENABLED", "false"
            buildConfigField "boolean", "FEATURE_CAST_ENABLED", "true"
            buildConfigField "boolean", "FEATURE_CLOUD_MESSAGING_ENABLED", "false"
            buildConfigField "boolean", "CRASHLYTICS_CRASH_REPORT_ENABLED", "true"
            resValue "string", "SNACKBAR_HINTS_ENABLED", "true"
        }
    }

    defaultConfig {
        testApplicationId 'org.catrobat.catroid.test'

        manifestPlaceholders = [ appName: appName,
                                 applicationIdForTests:"org.catrobat.catroid"]

        buildConfigField "boolean", "DATA_TRACKING", "false"
        buildConfigField "boolean", "CREATE_AT_SCHOOL", "false"
        buildConfigField "boolean", "RESTRICTED_LOGIN", "false"
        buildConfigField "boolean", "PHIRO_CODE", "false"

        buildConfigField "boolean", "CRASHLYTICS_CRASH_REPORT_ENABLED", "true"
        buildConfigField "boolean", "FEATURE_ACCESSIBILITY_SETTINGS_ENABLED", "true"
        buildConfigField "boolean", "FEATURE_TEMPLATES", "false"

        buildConfigField "String", "START_PROJECT", "\"No Starting Project\""
        buildConfigField "String", "PROJECT_NAME", "\"No Standalone Project\""
        buildConfigField "boolean", "FEATURE_APK_GENERATOR_ENABLED", "false"
    }

    productFlavors {
        catroid {
            applicationId appId
            buildConfigField "String", "START_PROJECT", "\"No Starting Project\""
            buildConfigField "String", "PROJECT_NAME", "\"No Standalone Project\""
            buildConfigField "boolean", "FEATURE_APK_GENERATOR_ENABLED", "false"
            buildConfigField "boolean", "FEATURE_CLOUD_MESSAGING_ENABLED", "false"
            buildConfigField "boolean", "CRASHLYTICS_CRASH_REPORT_ENABLED", "true"
        }

        standalone {
            applicationId appId
            versionCode 1
            versionName '1.0'

            buildConfigField "String", "START_PROJECT", "\"$projectId\""
            buildConfigField "String", "PROJECT_NAME", "\"$appName\""
            // here!
            buildConfigField "boolean", "FEATURE_APK_GENERATOR_ENABLED", "true"
            buildConfigField "boolean", "FEATURE_CLOUD_MESSAGING_ENABLED", "false"
            buildConfigField "boolean", "CRASHLYTICS_CRASH_REPORT_ENABLED", "false"
        }

        createatschool {
            appId = 'org.catrobat.catroid.createatschool'
            applicationId appId
            versionCode 12

            buildConfigField "boolean", "CREATE_AT_SCHOOL", "true"
            buildConfigField "boolean", "FEATURE_ACCESSIBILITY_SETTINGS_ENABLED", "true"
            buildConfigField "boolean", "RESTRICTED_LOGIN", "true"
            buildConfigField "boolean", "FEATURE_TEMPLATES", "true"

            resValue "string", "SNACKBAR_HINTS_ENABLED", "false"

            manifestPlaceholders = [ appName: appName,
                                     applicationIdForTests:"org.catrobat.catroid.createatschool" ]
        }

        phiro {
            appId = 'org.catrobat.catroid.phiro'
            applicationId appId
            versionCode 5

            buildConfigField "boolean", "PHIRO_CODE", "true"

            manifestPlaceholders = [ appName: appName,
                                     applicationIdForTests:"org.catrobat.catroid" ]
        }
    }
}

ext {
    projectVersion = "0.9"
}

configurations {
    compile.exclude group: 'org.apache.sanselan' //compile problem with parrot arsdk
    compile.exclude group: 'xpp3' //compile problem with xstream
    natives
}

dependencies {
    compile 'com.parrot:arsdk:3.12.6'
    //CAST
    compile 'com.android.support:mediarouter-v7:22.+'
    compile 'com.google.android.gms:play-services-cast:9.4.0'

    compile 'com.google.guava:guava:19.0'
    compile 'com.squareup.okhttp:okhttp:2.3.0'
    compile 'com.google.code.gson:gson:2.7'
    compile 'com.github.johnpersano:supertoasts:2.0@aar'
    compile 'com.github.mrengineer13:snackbar:1.2.0'

    compile 'com.koushikdutta.async:androidasync:2.+'
    compile 'com.sothree.slidinguppanel:library:3.3.0'
    compile 'com.squareup.picasso:picasso:2.5.2'

    compile ('com.thoughtworks.xstream:xstream:1.4.7') {
        exclude group: 'xmlpull'
    }
    compile 'com.github.oliewa92:MidiDroid:v1.1'

    def gdxVersion = '1.6.2'
    compile 'com.badlogicgames.gdx:gdx:' + gdxVersion
    compile 'com.badlogicgames.gdx:gdx-backend-android:' + gdxVersion
    natives 'com.badlogicgames.gdx:gdx-platform:' + gdxVersion + ':natives-x86'
    natives 'com.badlogicgames.gdx:gdx-platform:' + gdxVersion + ':natives-armeabi'
    natives 'com.badlogicgames.gdx:gdx-platform:' + gdxVersion + ':natives-armeabi-v7a'
    compile "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"
    natives "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-armeabi"
    natives "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-armeabi-v7a"
    natives "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-x86"

    compile 'com.facebook.android:facebook-android-sdk:4.14.1'
    compile 'com.android.support:support-v4:24.1.1'
    compile 'com.google.android.gms:play-services-auth:9.4.0'
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.github.billthefarmer:mididriver:v1.13'

    def retrofitVersion = '2.3.0'
    compile 'com.squareup.retrofit2:retrofit:' + retrofitVersion
    compile 'com.squareup.retrofit2:converter-gson:' + retrofitVersion
    compile 'com.squareup.retrofit2:adapter-rxjava2:'+ retrofitVersion
    compile 'io.reactivex.rxjava2:rxjava:2.1.0'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'

    def daggerVersion = '2.11'
    compile 'com.google.dagger:dagger:' + daggerVersion
    apt 'com.google.dagger:dagger-compiler:' + daggerVersion
    provided 'org.glassfish:javax.annotation:10.0-b28'
    androidTestApt 'com.google.dagger:dagger-compiler:' + daggerVersion
    androidTestCompile 'com.github.fabioCollini.daggermock:daggermock:0.8.1'

    compile group: 'ar.com.hjg', name: 'pngj', version: '2.1.0'
    compile fileTree(include: '*.jar', dir: 'src/main/libs')
    compile fileTree(include: '*.jar', dir: 'src/main/libs-natives')
    androidTestCompile fileTree(include: '*.jar', dir: 'src/androidTest/libs')
    androidTestCompile 'com.jayway.android.robotium:robotium-solo:5.2.1'
    androidTestCompile 'com.linkedin.dexmaker:dexmaker-mockito:2.2.0'
    androidTestCompile 'org.mockito:mockito-core:2.8.47'

    androidTestCompile ('com.android.support.test:runner:1.0.1') {
        exclude group: 'com.android.support'
    }
    androidTestCompile ('com.android.support.test.espresso:espresso-core:3.0.1') {
        exclude group: 'com.android.support'
    }
    androidTestCompile ('com.android.support.test.espresso:espresso-contrib:3.0.1') {
        exclude group: 'com.android.support'
    }

    compile ('com.android.support.test.espresso:espresso-intents:2.2.2') {
        exclude group: 'com.android.support'
    }

    compile ('com.android.support.test.espresso:espresso-core:3.0.1') {
        exclude group: 'com.android.support'
    }

    compile ('com.android.support.test.espresso:espresso-intents:2.2.2') {
        exclude group: 'com.android.support'
    }

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.8.47'

    pmd 'net.sourceforge.pmd:pmd:5.1.1'
    checkstyle 'com.puppycrawl.tools:checkstyle:7.6'
    compile('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true
    }
}

def getCurrentGitBranch() {
    try {
        return 'git rev-parse --abbrev-ref HEAD'.execute().text.trim()
    } catch (IOException exception) {
        throw new UnsupportedOperationException("Could not find git! Maybe it is not in \$PATH variable?", exception)
    }
}

android {
    compileSdkVersion 23
    buildToolsVersion '25.0.0'
    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 22
        applicationId appId
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        versionCode 39
        println "VersionCode is " + versionCode
        versionName "0.9.32"
        println "VersionName is " + versionName
        buildConfigField "String", "GIT_DESCRIBE", "\"${versionName}\""
        buildConfigField "String", "GIT_CURRENT_BRANCH", "\"${getCurrentGitBranch()}\""
        multiDexEnabled true
        manifestPlaceholders += [appName: manifestAppName, appIcon: manifestAppIcon]
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }

    lintOptions {
        // specific ignores should be defined via lint.xml file, all general ignores should be added here
        lintConfig file('config/lint.xml')
        // CommitPrefEdits should be reviewed, if using apply instead of commit is working with our tests
        // RtlSymmetry/RtlHardcoded should be reviewed
        // GradleDynamicVersion ignored - e.g. according to sdkmanager:gradle-plugin it should be imported this way
        // IconMissingDensityFolder - currently no xxxhdpi icons available
        // TODO ticket for HandlerLeak
        // TODO don't know if WrongRegion warning is a false-positive one
        ignore 'ContentDescription', 'InvalidPackage', 'ValidFragment', 'GradleDependency',
                'ClickableViewAccessibility', 'UnusedAttribute', 'CommitPrefEdits', 'OldTargetApi',
                'RtlSymmetry', 'GradleDynamicVersion', 'RtlHardcoded', 'HandlerLeak', 'IconMissingDensityFolder',
                'WrongRegion', 'RelativeOverlap', 'IconColors', 'MissingTranslation', 'ExtraTranslation',
                'GradleCompatible', 'WifiManagerLeak', 'ApplySharedPref', 'DefaultLocale', 'ObsoleteSdkInt',
                'StaticFieldLeak' , 'AppCompatResource'

        textReport true
        xmlReport true
        htmlReport true
        xmlOutput file("build/reports/lint-report.xml")
        htmlOutput file("build/reports/lint-report.html")
    }
}

task copyAndroidNatives() {
    file("src/main/jniLibs/armeabi/").mkdirs()
    file("src/main/jniLibs/armeabi-v7a/").mkdirs()
    file("src/main/jniLibs/x86/").mkdirs()
    configurations.natives.files.each { jar ->
        def outputDir = null
        if (jar.name.endsWith("natives-armeabi-v7a.jar")) outputDir = file("src/main/jniLibs/armeabi-v7a")
        if (jar.name.endsWith("natives-armeabi.jar")) outputDir = file("src/main/jniLibs/armeabi")
        if (jar.name.endsWith("natives-x86.jar")) outputDir = file("src/main/jniLibs/x86")
        if (outputDir != null) {
            copy {
                from zipTree(jar)
                into outputDir
                include "*.so"
            }
        }
    }
}

apply plugin: 'com.google.gms.google-services'
