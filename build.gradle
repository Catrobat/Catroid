import java.util.regex.Pattern

buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.+'
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.10.+'
    }
}

repositories {
    mavenCentral()
}

apply plugin: 'android-sdk-manager'
apply plugin: 'android'
apply plugin: 'com.android.application'
//apply plugin: 'findbugs'
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply from: 'gradle/adb_tasks.gradle'
apply from: 'gradle/code_quality_tasks.gradle'
apply from: 'gradle/intellij_config_tasks.gradle'

check.dependsOn 'checkstyle'
check.dependsOn 'pmd'

ext {
    projectVersion = "0.9"
    gdxVersion = '1.2.0'
    roboVMVersion = '0.0.14'
    featuresEnabled = [
            "lego_nxt"                          : true,
            "backpack"                          : false,
            "parrot_ar_drone"                   : false,
            "apk_generator"                     : false,
            "computer_vision_enhancement"       : true,
            "formula_editor_lists"              : false,
            "physics_engine_collision_filtering": false,
            "time_capsule"                      : false,
            "userbricks"                        : false
    ]
}

repositories {
    mavenLocal()
    mavenCentral()
    //maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://oss.sonatype.org/content/repositories/releases/' }
}

configurations {
    natives
    compile.exclude group: 'xpp3' //compile problem with xstream
}

dependencies {
    compile 'com.actionbarsherlock:actionbarsherlock:4.4.0@aar'
    compile 'com.android.support:support-v4:19.1.0'
    compile "com.badlogicgames.gdx:gdx:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx-backend-android:$gdxVersion"
    compile 'com.google.guava:guava:17.0'
    androidTestCompile 'com.jayway.android.robotium:robotium-solo:5.2.1'
    natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi"
    natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi-v7a"
    natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86"
    compile fileTree(dir: 'catroid/libs', include: '*.jar')
    compile fileTree(dir: 'catroid/libs-natives', include: '*.jar')
    compile fileTree(dir: 'catroidTest/libs', include: '*.jar')
    pmd 'net.sourceforge.pmd:pmd:5.1.1'
}


def getBuildNumberParameter = { ->
    def code = project.hasProperty('versionCode') ? versionCode.toInteger() : -1
    return code
}

def generateVersionName(version, buildNumber) {
    def versionName
    if (buildNumber == -1) {
        versionName = getGitDescribe() + " " + getCurrentGitBranch()
    } else {
        versionName = version + "." + buildNumber
    }
    return versionName
}

def getGitDescribe() {
    try {
        return 'git describe --tags'.execute().text.trim()
    } catch (IOException exception) {
        throw new UnsupportedOperationException("Could not find git! Maybe it is not in \$PATH variable?", exception)
    }
}

def getCurrentGitBranch() {
    try {
        return 'git rev-parse --abbrev-ref HEAD'.execute().text.trim()
    } catch (IOException exception) {
        throw new UnsupportedOperationException("Could not find git! Maybe it is not in \$PATH variable?", exception)
    }
}


android {
    compileSdkVersion 19
    buildToolsVersion '19.1.0'
    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 19
        applicationId 'org.catrobat.catroid'
        testApplicationId "org.catrobat.catroid.test"
        testInstrumentationRunner 'pl.polidea.instrumentation.PolideaInstrumentationTestRunner'
        versionCode getBuildNumberParameter()
        println "VersionCode is " + versionCode
        versionName generateVersionName(projectVersion, versionCode)
        println "VersionName is " + versionName
        buildConfigField "String", "GIT_DESCRIBE", "\"${getGitDescribe()}\""
        buildConfigField "String", "GIT_CURRENT_BRANCH", "\"${getCurrentGitBranch()}\""
    }

    sourceSets {
        main {
            manifest.srcFile 'catroid/AndroidManifest.xml'
            java.srcDirs = ['catroid/src']
            resources.srcDirs = ['catroid/src']
            aidl.srcDirs = ['catroid/src']
            renderscript.srcDirs = ['catroid/src']
            res.srcDirs = ['catroid/res']
            assets.srcDirs = ['catroid/assets']
        }

        androidTest {
            java.srcDirs = ['catroidTest/src']
            resources.srcDirs = ['catroidTest/src']
            aidl.srcDirs = ['catroidTest/src']
            renderscript.srcDirs = ['catroidTest/src']
            res.srcDirs = ['catroidTest/res']
            assets.srcDirs = ['catroidTest/assets']
            if (file('testexclusions.txt').exists()) {
                java.exclude file('testexclusions.txt').readLines()
            }


            packagingOptions {
                exclude 'lib/armeabi/libgdx.so'
                exclude 'lib/armeabi-v7a/libgdx.so'
                exclude 'lib/x86/libgdx.so'
            }
        }
    }
    lintOptions {
        // define ignores like a specific res-folder in our lint.xml file
        lintConfig file('config/lint.xml')

        // all general ignores should be added in this place
        // CommitPrefEdits should be reviewed, if using apply instead of commit is working with our tests
        // OldTargetApi should be reviewed - consider updating target API to 20
        ignore 'ContentDescription', 'InvalidPackage', 'ValidFragment', 'GradleDependency',
                'ClickableViewAccessibility', 'UnusedAttribute', 'CommitPrefEdits', 'OldTargetApi'

        textReport true
        xmlReport true
        htmlReport false
        xmlOutput file("build/reports/lint.xml")
    }
}
// needed to add JNI shared libraries to APK when compiling on CLI
tasks.withType(com.android.build.gradle.tasks.PackageApplication) { pkgTask ->
    pkgTask.jniFolders = new HashSet<File>()
    pkgTask.jniFolders.add(new File(projectDir, 'libs'))
}

// called every time gradle gets executed, takes the native dependencies of
// the natives configuration, and extracts them to the proper libs/ folders
// so they get packed with the APK.
task copyAndroidNatives() {
    file("libs/armeabi/").mkdirs();
    file("libs/armeabi-v7a/").mkdirs();
    file("libs/x86/").mkdirs();

    configurations.natives.files.each { jar ->
        def outputDir = null
        if(jar.name.endsWith("natives-armeabi-v7a.jar")) outputDir = file("libs/armeabi-v7a")
        if(jar.name.endsWith("natives-armeabi.jar")) outputDir = file("libs/armeabi")
        if(jar.name.endsWith("natives-x86.jar")) outputDir = file("libs/x86")
        if(outputDir != null) {
            copy {
                from zipTree(jar)
                into outputDir
                include "*.so"
            }
        }
    }
}

task run(type: Exec) {
    def path
    def localProperties = project.file("../local.properties")
    if (localProperties.exists()) {
        Properties properties = new Properties()
        localProperties.withInputStream { instr ->
            properties.load(instr)
        }
        def sdkDir = properties.getProperty('sdk.dir')
        if (sdkDir) {
            path = sdkDir
        } else {
            path = "$System.env.ANDROID_HOME"
        }
    } else {
        path = "$System.env.ANDROID_HOME"
    }

    def adb = path + "/platform-tools/adb"
    commandLine "$adb", 'shell', 'am', 'start', '-n', 'org.catrobat.catroid.android/org.catrobat.catroid.android.AndroidLauncher'
}


if (project.hasProperty('jenkins')) {
    project.android.dexOptions.preDexLibraries = false
//    android.buildTypes.each { type ->
//        if (type.name == "debug") {
//            type.packageNameSuffix = ".jenkinsdebug"
//            type.versionNameSuffix = "-jenkins-$type.name"
//        }
//    }
} else {
    //not a jenkins build, remove animation scale permission
    android.applicationVariants.all { variant ->
        println "Removing the SET_ANIMATION_SCALE permission for $variant.name"
        variant.processManifest.doLast {
            def generatedContent = manifestOutputFile.getText()
            generatedContent = generatedContent.replace('<uses-permission android:name="android.permission.SET_ANIMATION_SCALE" />', '')
            if (generatedContent.contains('SET_ANIMATION_SCALE')) {
                throw new RuntimeException("Error in removing animation scale permission!")
            }
            manifestOutputFile.write(generatedContent)
        }
    }
}

task featuresToBuildconfig << {
    println "Activated Features:"
    for (feature in featuresEnabled) {
        def name = feature.key
        def value = feature.value
        if (project.hasProperty("allFeatures_enabled"))
            value = project["allFeatures_enabled"]
        if (project.hasProperty("${name}_enabled"))
            value = project["${name}_enabled"]

        if (!(value.toString().equals("true") || value.toString().equals("false")))
            throw new IllegalArgumentException("Wrong Argument! Usage:\ne.g. -PallFeatures_enabled=true -Pparrot_ar_drone_enabled=false")

        if (value.toString().equals("true")) {
            println "- " + name
        }
        android.defaultConfig.buildConfigField "boolean", "FEATURE_${name.toUpperCase()}_ENABLED", "${value}"
    }
    println ""
}

task testManifestHack << {
    def origManifest = file('catroidTest/AndroidManifest.xml')
    def generatedManifest = file("build/intermediates/manifests/test/debug/AndroidManifest.xml")
    def origContent = origManifest.getText()
    def generatedContent = generatedManifest.getText()
    def pattern = Pattern.compile("<application.*?>.*?</application>", Pattern.DOTALL)
    def matcher = pattern.matcher(origContent)
    matcher.find()
    origContent = matcher.group()
    generatedContent = pattern.matcher(generatedContent).replaceAll(origContent)
    generatedManifest.write(generatedContent)
}

gradle.projectsEvaluated {
    generateDebugTestBuildConfig.dependsOn testManifestHack
}

preBuild.dependsOn featuresToBuildconfig

def signing_config_file = file(System.getProperty("user.home") + "/.catrobat/catroid_signing_config.gradle")
if (signing_config_file.exists()) {
    apply from: signing_config_file.absolutePath
}

